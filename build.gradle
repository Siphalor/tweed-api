plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id 'maven-publish'
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '4.0.0'
}

repositories {
	mavenCentral()
	maven { url "https://minecraft.curseforge.com/api/maven"}
	maven {
		name "Jitpack"
		url "https://jitpack.io/"
	}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
	refmapName = "${project.mod_id}-refmap.json"
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${minecraft_version}+build.${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	modCompile "net.fabricmc.fabric-api:fabric-networking-v0:0.1.7+003643510c"
	include "net.fabricmc.fabric-api:fabric-networking-v0:0.1.7+003643510c"
	modCompile "net.fabricmc.fabric-api:fabric-api-base:0.1.2+b7f9825d0c"
	include "net.fabricmc.fabric-api:fabric-api-base:0.1.2+b7f9825d0c"
	modCompile "net.fabricmc.fabric-api:fabric-resource-loader-v0:0.1.10+06c939b30c"
	include "net.fabricmc.fabric-api:fabric-resource-loader-v0:0.1.10+06c939b30c"
	modCompile "net.fabricmc.fabric-api:fabric-events-lifecycle-v0:0.1.2+003643510c"
	include "net.fabricmc.fabric-api:fabric-events-lifecycle-v0:0.1.2+003643510c"

	// hjson
	//compile 'org.hjson:hjson:3.0.0'
	shadow 'com.github.Siphalor:hjson-java:master-SNAPSHOT'
	implementation 'com.github.Siphalor:hjson-java:master-SNAPSHOT'
	
	// Jankson
	shadow 'blue.endless:jankson:1.1.0'
	implementation 'blue.endless:jankson:1.1.0'

	modApi ("io.github.prospector:modmenu:${project.modmenu_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
	modApi "me.shedaniel.cloth:basic-math:0.5.0"
	
	// Toml
	//shadow 'com.moandjiezana.toml:toml4j:0.7.2'
	//modImplementation 'com.moandjiezana.toml:toml4j:0.7.2'

	// Cloth Config
    modApi("me.shedaniel.cloth:config-2:${project.clothconfig_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	testmodCompile sourceSets.main.output
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	archiveClassifier.set("")
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("dev")
	relocate 'org.hjson', 'de.siphalor.tweed.shadow.org.hjson'
	relocate 'blue.endless.jankson', 'de.siphalor.tweed.shadow.blue.endless.jankson'
	//relocate 'com.moandjiezana.toml', 'de.siphalor.tweed.shadow.com.moandjiezana.toml'
}

remapJar {
	dependsOn(shadowJar)
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = "${project.archives_base_name}-${project.minecraft_major_version}"
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (project.hasProperty("siphalorMavenUser")) {
			maven {
				url = "https://maven.siphalor.de/upload.php/"
				name = "Siphalor"
				credentials {
					username = siphalorMavenUser
					password = siphalorMavenPassword
				}
			}
		}
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
